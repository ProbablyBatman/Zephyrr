apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def buildNumber = System.getenv("BITRISE_BUILD_NUMBER") as Integer ?: 0

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "greenberg.moviedbshell"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode buildNumber
        versionName "0.2.$buildNumber"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        buildTypes.each {
            it.buildConfigField 'String', 'TMDB_API_KEY', "\"$System.env.TMDB_API_KEY\""
        }
    }
    configurations {
        ktlint
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$currentVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    kapt 'com.github.bumptech.glide:compiler:4.7.1'
    implementation 'com.hannesdorfmann.mosby3:mvp:3.1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation "com.android.support:appcompat-v7:$currentVersion"
    implementation "com.android.support:recyclerview-v7:$currentVersion"
    implementation "com.android.support:cardview-v7:$currentVersion"
    implementation "com.android.support:palette-v7:$currentVersion"
    implementation "com.android.support:design:$currentVersion"
    implementation "com.android.support:support-v4:$currentVersion"
    implementation "com.android.support:support-media-compat:$currentVersion"

    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"

    implementation 'com.google.dagger:dagger:2.17'
    kapt 'com.google.dagger:dagger-compiler:2.17'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5') {
        transitive = true
    }

    ktlint "com.github.shyiko:ktlint:0.28.0"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

